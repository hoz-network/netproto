plugins {
    id 'java'
    id 'java-library'
    id 'idea'
    id 'org.screamingsandals.plugin-builder' version '1.0.67'
    id "com.google.protobuf" version "0.8.18"
}

group 'net.hoz'
version '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url = "https://repo.screamingsandals.org/public"
    }
}

dependencies {
    api group: 'org.slf4j', name: 'slf4j-api', version: SLF4J_VERSION

    //reactor
    api group: 'io.projectreactor', name: 'reactor-core', version: REACTOR_VERSION

    //grpc
    api group: 'io.rsocket.rpc', name: 'rsocket-rpc-core', version: RSOCKET_RPC_VERSION
    api group: 'io.rsocket.rpc', name: 'rsocket-rpc-protobuf', version: RSOCKET_RPC_VERSION

    //proto
    api group: 'com.google.protobuf', name: 'protobuf-java', version: PROTOBUF_VERSION

    //screamingLib
    api group: "org.screamingsandals.lib", name: "core-common", version: SLIB_VERSION
    api group: "org.screamingsandals.lib", name: "tasker-common", version: SLIB_VERSION

    api group: 'com.iamceph.resulter', name: 'resulter-core', version: RESULTER_VERSION
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

sourceSets.configureEach{s->
    java {
        srcDir project.buildDir.toPath()
                .resolve("generatedProto")
                .resolve(s.name)
                .resolve("java")
                .toFile()

        srcDir project.buildDir.toPath()
                .resolve("generatedProto")
                .resolve(s.name)
                .resolve("grpc")
                .toFile()
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${PROTOBUF_VERSION}"
    }

    plugins {
        rsocketRpc {
            artifact = "io.rsocket.rpc:rsocket-rpc-protobuf:${RSOCKET_RPC_VERSION}"
        }
    }

    generateProtoTasks {
        all()*.plugins {
            rsocketRpc {}
        }
    }

    generatedFilesBaseDir = "${project.buildDir}/generatedProto"
}